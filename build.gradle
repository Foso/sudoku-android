/*
 * Copyright 2018 Google LLC
 *  
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *  
 *     http://www.apache.org/licenses/LICENSE-2.0
 *  
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *  
 */

buildscript {

    def version = 4

    ext.buildConfig = [
            'compileSdk': 27,
            'minSdk': 21,
            'targetSdk': 27,
            'version': version,
            'versionName': "1.0-${version}",
    ]

    ext.versions = [
            'buildTools': '28.0.0-rc2',
            'supportLibrary': '27.1.1',
            'constraintLayout': '1.1.0',
            'instantApps': '1.1.0',
            'lifecycles': '1.1.1',
            'androidktx': '0.3',
            'playServices': '12.0.1',
            'firebaseuiauth': '3.3.0',
            'gson': '2.8.2',
            'kotlin': '1.2.41',
            'dagger': '2.14.1',
    ]


    repositories {
        jcenter()
        google()
    }
    dependencies {
        //noinspection GradleDependency
        classpath 'com.android.tools.build:gradle:3.2.0-alpha14'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'com.google.gms:google-services:3.2.1'
        classpath "com.google.gms:oss-licenses:0.9.2"
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'com.android.support') {
                    details.useVersion versions.supportLibrary
                } else if (details.requested.group == 'com.google.firebase') {
                    details.useVersion versions.playServices
                }
            }
        }
    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}
