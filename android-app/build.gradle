/*
 * Copyright 2018 Google LLC
 *  
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *  
 *     http://www.apache.org/licenses/LICENSE-2.0
 *  
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *  
 */

apply plugin: 'com.android.feature'
//apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    baseFeature true

    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        versionCode 1
        versionName "1.0"
        minSdkVersion 21
        targetSdkVersion 27
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        consumerProguardFiles 'proguard.pro'
    }

    dataBinding {
        enabled true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debugMini {
            initWith debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    "proguard.pro"
            matchingFallbacks = ['debug']
        }

        debugMiniOptim {
            initWith debugMini
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    "proguard.pro", "aggressive.pro"
            matchingFallbacks = ['debug']
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    "proguard.pro"
        }

        releaseOptim {
            initWith release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    "proguard.pro", "aggressive.pro"
            matchingFallbacks = ['release']
            consumerProguardFiles 'proguard.pro', 'aggressive.pro'
        }
    }
}

dependencies {
    application project(':android-installedapp')

    api project(":libsudoku-jvm")
    api 'com.android.support:appcompat-v7:27.0.2'
    api 'com.android.support.constraint:constraint-layout:1.0.2'
    api 'com.android.support:support-annotations:27.0.2'
    api 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.google.android.instantapps:instantapps:1.1.0'

    api "android.arch.lifecycle:extensions:1.1.0"

    implementation 'androidx.core:core-ktx:0.1'

    implementation 'com.google.android.gms:play-services-oss-licenses:11.8.0'

    implementation 'com.google.code.gson:gson:2.8.2'

    api 'com.google.dagger:dagger:2.14.1'
    api 'com.google.dagger:dagger-android-support:2.14.1'
    kapt 'com.google.dagger:dagger-compiler:2.14.1'
    kapt 'com.google.dagger:dagger-android-processor:2.14.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
